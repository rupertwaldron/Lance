plugins {
    id 'org.springframework.boot' version '2.6.6'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'java-library'
    id 'maven-publish'
}

group = 'com.ruppyrup'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-autoconfigure'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.13.2.2'
    api 'io.projectreactor:reactor-core:3.4.17'
    compileOnly 'org.projectlombok:lombok:1.18.22'
    annotationProcessor 'org.projectlombok:lombok:1.18.22'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
    testImplementation 'io.cucumber:cucumber-java:7.2.3'
    testImplementation 'io.cucumber:cucumber-junit:7.2.3'
}

test {
    useJUnitPlatform() {
        exclude '**/integration/*'
    }
}

task integrationTest(type: Test) {
    useJUnitPlatform() {
        include '**/integration/*'
    }
}

task RunCukesTest(type: Test) {
    include '**/runners/*Runner.class'
}

/**
 * use below to publishToMavenLocal .m2 directory
 */

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
        }
    }
}

task cleanBuildAllTestsPublish {
    dependsOn 'clean'
    dependsOn 'build'
    dependsOn 'integrationTest'
    dependsOn 'RunCukesTest'
    dependsOn 'publishToMavenLocal'
    tasks.findByName('build').mustRunAfter 'clean'
    tasks.findByName('integrationTest').mustRunAfter 'build'
    tasks.findByName('RunCukesTest').mustRunAfter 'integrationTest'
    tasks.findByName('publishToMavenLocal').mustRunAfter 'RunCukesTest'
}
